apply plugin: 'com.android.application'

import com.android.build.OutputFile

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.github.axet.torrentclient"
        minSdkVersion 15
        targetSdkVersion 23 // 24+ unable to share 'resource/folder'
        versionCode 370
        versionName "2.7.71"
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.packageApplication.outputFile
            def abi = output.getFilter(OutputFile.ABI)
            if (abi == null)
                abi = "universal";
            def fileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + abi + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
            output.packageApplication.outputFile = new File(file.parent, fileName)
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "mips"
            universalApk true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:25.3.1'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.getbase:floatingactionbutton:1.10.1' // API 14+
    compile 'org.apmem.tools:layouts:1.10@aar' // API 14+
    compile('com.mikepenz:materialdrawer:5.4.0@aar') { transitive = true } // API 10+
    compile('com.github.axet:wget:1.4.2') {
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }
    compile 'com.github.axet:android-library:1.12.39' // compile project(':android-library')
    compile 'com.github.axet:libtorrent:0.7.6@aar' // compile project(":libtorrent") // API 15+
    compile ('com.github.axet:java-unrar:1.7.0-1') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
//    compile 'com.subgraph.orchid:orchid:0.9.1'
//    compile 'com.madgag.spongycastle:prov:1.54.0.0'
}
